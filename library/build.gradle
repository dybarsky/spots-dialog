ext.ARTIFACT_ID = "spots-dialog"

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

// configure android plugin

android {
    compileSdkVersion Integer.parseInt(COMPILE_SDK_VERSION)
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(TARGET_SDK_VERSION)
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}


// tasks for packaging source and javadocs artifacts

task javadocGenerate(type: Javadoc) {
    // prints javadoc outputs when info logging level set
    logging.captureStandardError LogLevel.INFO

    description = "Generates javadoc"
    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocPackage(dependsOn: javadocGenerate, type: Jar) {
    baseName = ARTIFACT_ID
    classifier = "javadoc"
    version VERSION_NAME
    from tasks.javadocGenerate.destinationDir
}

task sourcePackage(type: Jar) {
    baseName = ARTIFACT_ID
    classifier = "sources"
    version VERSION_NAME
    from android.sourceSets.main.java.srcDirs
}

//configure sign plugin

artifacts {
    archives javadocPackage, sourcePackage
}

signing {
    sign configurations.archives
}

// configure maven plugin

def getRepoUrl() {
    if (VERSION_NAME.contains("SNAPSHOT")) {
        return "https://oss.sonatype.org/content/repositories/snapshots/"
    } else {
        return "https://oss.sonatype.org/service/local/staging/deploy/maven2"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: getRepoUrl()) {
                authentication(userName: OSS_USERNAME, password: OSS_PASSWORD)
            }
            pom.project {
                groupId "com.github.d-max"
                artifactId "spots-dialog"
                version VERSION_NAME
                packaging "aar"
                name "spots progress dialog"
                description "Android AlertDialog with moving spots progress indicator"
                url "https://github.com/d-max/spots-dialog"

                scm {
                    url "https://github.com/d-max/spots-dialog"
                    connection "scm:git@github.com:d-max/spots-dialog.git"
                    developerConnection "scm:git@github.com:d-max/spots-dialog.git"
                }

                licenses {
                    license {
                        name "MIT License"
                        url "http://www.opensource.org/licenses/mit-license.php"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "d-max"
                        name "Maxim Dybarsky"
                        email "maxim.dybarskyy@gmail.com"
                    }
                }
            }
        }
    }
}